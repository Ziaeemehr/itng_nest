
neuron olm_receptors:
  state:
    r integer # number of steps in the current refractory phase
  end

  initial_values:
    V_m mV = -63.0 mV # Membrane potential

    function alpha_n_init real = 0.018 * (V_m / mV - 25.0) / (1.0 - exp(-(V_m / mV - 25.0) / 25.0))
    function beta_n_init  real = 0.0036 * (35.0 - V_m / mV) / (1.0 - exp(-(35.0 - V_m / mV) / 12.0))
    
    function alpha_m_init real = 0.1*(V_m/mV+38.0)/(1.0-exp(-0.1*(V_m/mV+38.0)))
    function beta_m_init  real = 4.0 * exp(-(V_m / mV + 65.0) / 18.0)
    
    function alpha_h_init real = 0.07 * exp(-(V_m / mV + 63.0) / 20.0)
    function beta_h_init  real = 1.0 / (exp(-(V_m / mV + 33.0) / 10.0) + 1.0)
    
    function r_h_inf_init real = 1. / (1.0 + exp((V_m / mV + 84.) / 10.2))

    function Act_A_inf_init real = 1.0 / (1.0 + exp(-(V_m/mV + 14.0) / 16.6))
    function Act_B_inf_init real = 1.0/(1.0 + exp((V_m/mV + 71.0)/7.3))    

    Inact_h real = alpha_h_init / ( alpha_h_init + beta_h_init )    # Inactivation variable h for Na
    Act_n real = alpha_n_init / (alpha_n_init + beta_n_init)  # Activation variable n for K 
    Act_r_h real = r_h_inf_init 
    Act_A real = Act_A_inf_init
    Act_B real = Act_B_inf_init

    g_AMPA nS = 0.0 nS
    g_AMPA' nS/ms = 0.0 nS/ms

    g_NMDA nS = 0.0 nS 
    g_NMDA' nS/ms = 0.0 nS/ms 

    g_GABAA nS = 0.0 nS
    g_GABAA' nS/ms = 0.0 nS/ms
    
    g_GABAB nS = 0.0 nS 
    g_GABAB' nS/ms = 0.0 nS/ms

  end

  equations:
    
    function I_syn_ampa pA = -g_AMPA * ( V_m - AMPA_E_rev )
    function I_syn_nmda pA = -g_NMDA * ( V_m - NMDA_E_rev ) / ( 1 + exp( ( NMDA_Vact - V_m ) / NMDA_Sact ) )
    function I_syn_gaba_a pA = -g_GABAA * ( V_m - GABA_A_E_rev )
    function I_syn_gaba_b pA = -g_GABAB * ( V_m - GABA_B_E_rev )
    recordable function I_syn pA = I_syn_ampa + I_syn_nmda + I_syn_gaba_a + I_syn_gaba_b
    
    function alpha_n real = 0.018 * (V_m / mV - 25.0) / (1.0 - exp(-(V_m / mV - 25.0) / 25.0))
    function beta_n  real = 0.0036 * (35.0 - V_m / mV) / (1.0 - exp(-(35.0 - V_m / mV) / 12.0))
    
    function alpha_m real = 0.1*(V_m/mV+38.0)/(1.0-exp(-0.1*(V_m/mV+38.0)))
    function beta_m  real = 4.0 * exp(-(V_m / mV + 65.0) / 18.0)
    
    function alpha_h real = 0.07 * exp(-(V_m / mV + 63.0) / 20.0)
    function beta_h  real = 1.0 / (exp(-(V_m / mV + 33.0) / 10.0) + 1.0)

    function Act_m_inf real = alpha_m / (alpha_m + beta_m)
    function I_Na  pA = g_Na * Act_m_inf * Act_m_inf * Act_m_inf * Inact_h * ( V_m - E_Na )
    function I_K   pA  = g_K * Act_n * Act_n * Act_n * Act_n * ( V_m - E_K )
    function I_L   pA = g_L * ( V_m - E_L )
    function I_H   pA = g_H * Act_r_h * (V_m - E_H)
    function I_A   pA = g_A * Act_A * Act_B * (V_m - E_A)

    function r_h_inf real = 1. / (1.0 + exp((V_m / mV + 84.) / 10.2))
    function tau_r_h real = 1. /(exp(-14.59-0.086*V_m/mV)+exp(-1.87+0.0701*V_m/mV))

    function Act_A_inf real = 1.0 / (1.0 + exp(-(V_m/mV + 14.0) / 16.6))
    function Act_B_inf real = 1.0/(1.0 + exp((V_m/mV + 71.0)/7.3))

    function tau_A_A real = 5.0
    function tau_B_A real = 1./(.000009/exp((V_m/mV-26.)/28.5)+.014/(.2+exp(-(V_m/mV + 70.)/11.)))


    Inact_h' = ( alpha_h * ( 1 - Inact_h ) - beta_h * Inact_h ) / ms # h-variable
    Act_n' = ( alpha_n * ( 1 - Act_n ) - beta_n * Act_n ) / ms # n-variable
    Act_r_h' = (r_h_inf - Act_r_h) / tau_r_h / ms # r-variable
    Act_A' = (Act_A_inf - Act_A) / tau_A_A / ms   # A-variable
    Act_B' = (Act_B_inf - Act_B) / tau_B_A / ms   # B-variable
    V_m' =( -( I_Na + I_K + I_L + I_H + I_A ) + I_e + I_stim + I_syn ) / C_m
    
    #############
    # Synapses
    #############
    
    g_AMPA'' = -g_AMPA' / AMPA_Tau_1
    g_AMPA' = g_AMPA' - g_AMPA  / AMPA_Tau_2

    g_NMDA'' = -g_NMDA' / NMDA_Tau_1
    g_NMDA' = g_NMDA' - g_NMDA / NMDA_Tau_2

    g_GABAA'' = -g_GABAA' / GABA_A_Tau_1
    g_GABAA' = g_GABAA' - g_GABAA / GABA_A_Tau_2

    g_GABAB'' = -g_GABAB' / GABA_B_Tau_1
    g_GABAB' = g_GABAB' - g_GABAB /GABA_B_Tau_2

  end

  parameters:
    t_ref ms = 2.0 ms      # Refractory period 2.0
    g_Na nS = 3000.0 nS    # Sodium peak conductance
    g_K nS = 2300.0 nS      # Potassium peak conductance
    g_L nS = 5.0 nS          # Leak conductance
    g_H nS = 1200.0 nS
    g_A nS = 2200.0 nS
    C_m pF = 130.0 pF       # Membrane Capacitance
    E_Na mV = 90.0 mV         # Sodium reversal potential
    E_K mV = -100.0 mV        # Potassium reversal potentia
    E_L mV = -70.0 mV     # Leak reversal Potential (aka resting potential)
    E_H mV = -32.9 mV    # 
    E_A mV = -90.0 mV
    V_Tr mV = -20.0 mV        # Spike Threshold
    
    # Parameters for synapse of type AMPA, GABA_A, GABA_B and NMDA
    AMPA_g_peak nS = 0.1 nS      # peak conductance
    AMPA_E_rev mV = 0.0 mV       # reversal potential
    AMPA_Tau_1 ms = 0.5 ms       # rise time
    AMPA_Tau_2 ms = 2.4 ms       # decay time, Tau_1 < Tau_2
    
    NMDA_g_peak nS = 0.075 nS    # peak conductance
    NMDA_Tau_1 ms = 4.0 ms       # rise time
    NMDA_Tau_2 ms = 40.0 ms      # decay time, Tau_1 < Tau_2
    NMDA_E_rev mV = 0.0 mV       # reversal potential
    NMDA_Vact mV = -58.0 mV      # inactive for V << Vact, inflection of sigmoid
    NMDA_Sact mV = 2.5 mV        # scale of inactivation
    
    GABA_A_g_peak nS = 0.33 nS   # peak conductance
    GABA_A_Tau_1 ms = 1.0 ms     # rise time
    GABA_A_Tau_2 ms = 7.0 ms     # decay time, Tau_1 < Tau_2
    GABA_A_E_rev mV = -70.0 mV   # reversal potential
    
    GABA_B_g_peak nS = 0.0132 nS # peak conductance
    GABA_B_Tau_1 ms = 60.0 ms    # rise time
    GABA_B_Tau_2 ms = 200.0 ms   # decay time, Tau_1 < Tau_2
    GABA_B_E_rev mV = -90.0 mV   # reversal potential for intrinsic current

    # constant external input current
    I_e pA = 0 pA
  
  end

  internals:
    
    AMPAInitialValue real = compute_synapse_constant( AMPA_Tau_1, AMPA_Tau_2, AMPA_g_peak )
    NMDAInitialValue real = compute_synapse_constant( NMDA_Tau_1, NMDA_Tau_2, NMDA_g_peak )
    GABA_AInitialValue real = compute_synapse_constant( GABA_A_Tau_1, GABA_A_Tau_2, GABA_A_g_peak )
    GABA_BInitialValue real = compute_synapse_constant( GABA_B_Tau_1, GABA_B_Tau_2, GABA_B_g_peak )
    RefractoryCounts integer = steps(t_ref) # refractory time in steps
  
  end

  input:
    AMPA nS  <- spike
    NMDA nS  <- spike
    GABA_A nS <- spike
    GABA_B nS <- spike
    I_stim pA <- current
  end

  output: spike

  update:
    U_old mV = V_m
    integrate_odes()
    
    g_AMPA' += AMPAInitialValue * AMPA /ms
    g_NMDA' += NMDAInitialValue * NMDA /ms
    g_GABAA' += GABA_AInitialValue * GABA_A /ms
    g_GABAB' += GABA_BInitialValue * GABA_B /ms
    
    # sending spikes: 
    if r > 0: # is refractory?
      r -= 1
    elif V_m > V_Tr and U_old > V_Tr: # threshold && maximum
      r = RefractoryCounts
      emit_spike()
    end

  end


  function compute_synapse_constant(Tau_1 ms, Tau_2 ms, g_peak real) real:
    # Factor used to account for the missing 1/((1/Tau_2)-(1/Tau_1)) term
    # in the ht_neuron_dynamics integration of the synapse terms.
    # See: Exact digital simulation of time-invariant linear systems
    # with applications to neuronal modeling, Rotter and Diesmann,
    # section 3.1.2.
    exact_integration_adjustment real = ( ( 1 / Tau_2 ) - ( 1 / Tau_1 ) ) * ms

    t_peak real = ( Tau_2 * Tau_1 ) * ln( Tau_2 / Tau_1 ) / ( Tau_2 - Tau_1 ) / ms
    normalisation_factor real = 1 / ( exp( -t_peak / Tau_1 ) - exp( -t_peak / Tau_2 ) )

    return g_peak * normalisation_factor * exact_integration_adjustment
  end

end


